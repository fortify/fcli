plugins {
	id('com.github.jk1.dependency-license-report') version '1.16'
	id "org.kordamp.gradle.markdown" version "2.2.0"
	id("com.github.johnrengelman.shadow") version "7.0.0"
	id("io.micronaut.graalvm") version "2.0.6" // Needed to generate Graal resource-config.json
	id("io.micronaut.application") version "2.0.6" apply false
	id("io.micronaut.library") version "2.0.6" apply false
	id 'eclipse'
	id "com.github.ben-manes.versions" version "0.42.0"
}

version = "0.1"
group = "com.fortify.cli"

apply plugin: "io.micronaut.application"
subprojects {
	apply plugin: "io.micronaut.library"
	apply plugin: 'eclipse'
}

application {
	mainClass.set("com.fortify.cli.app.FortifyCLI")
}
shadowJar {
	archiveBaseName.set('fcli')
	archiveClassifier.set('')
	archiveVersion.set('')
}

allprojects {
	java {
		sourceCompatibility = JavaVersion.toVersion("11")
		targetCompatibility = JavaVersion.toVersion("11")
	}
	micronaut {
		testRuntime("junit5")
		processing {
			incremental(true)
			annotations("com.fortify.cli.*")
		}
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		// Lombok dependency & annotation processor
		compileOnly 'org.projectlombok:lombok:1.16.20'
		annotationProcessor "org.projectlombok:lombok:1.16.20"

		// Picocli dependency and annotation processor
		annotationProcessor("info.picocli:picocli-codegen:4.6.3")
		implementation("info.picocli:picocli:4.6.3")

		annotationProcessor("io.micronaut:micronaut-inject-java")
		annotationProcessor("io.micronaut:micronaut-graal")

		// Micronaut dependencies
		implementation("io.micronaut:micronaut-runtime")
		implementation("io.micronaut.picocli:micronaut-picocli")
		implementation("javax.annotation:javax.annotation-api")

		// Logback dependency to allow us to configure logging programmatically
		implementation("ch.qos.logback:logback-classic:1.2.6")

		implementation('com.konghq:unirest-java:3.13.0') {
			exclude group: 'com.google.code.gson', module: 'gson' // We use Jackson, so no need for Gson
		}
		implementation('com.konghq:unirest-objectmapper-jackson:3.13.0')

		implementation('com.jayway.jsonpath:json-path:2.6.0') {
			//exclude group: 'net.minidev', module: 'json-smart'
		}

		// for yaml support and generating trees/tables
		implementation('com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.0')
		implementation('com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.13.0')
		implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.13.0")
		implementation('hu.webarticum:tree-printer:1.3')
		implementation('com.github.freva:ascii-table:1.2.0')
		
		// Encryption library
		implementation('org.jasypt:jasypt:1.9.3:lite')
	}
}
configure(allprojects.findAll {it.name != 'fcli-common'}) {
	dependencies {
		implementation project(':fcli-common')
	}
}

dependencies {
	runtimeOnly project(':fcli-ssc')
	runtimeOnly project(':fcli-fod')
	runtimeOnly project(':fcli-sc-sast')
	runtimeOnly project(':fcli-sc-dast')
	runtimeOnly project(':fcli-run')

	// Logging dependencies
	runtimeOnly("ch.qos.logback:logback-classic:1.2.6")
	runtimeOnly("org.slf4j:jcl-over-slf4j:1.7.32")

	// GraalVM dependency
	compileOnly("org.graalvm.nativeimage:svm")
}

ext {
	gradleHelpersLocation = "https://raw.githubusercontent.com/fortify-ps/gradle-helpers/1.6"
	thirdPartyBaseName = "${rootProject.name}"
}
apply from: "${gradleHelpersLocation}/thirdparty-helper.gradle"
apply from: "${gradleHelpersLocation}/readme2html.gradle"

task dist(type: Copy) {
	dependsOn 'build', 'readme2html'
	from "${buildDir}/html"
	from("${projectDir}") {
		include "config/**/*"
		include "LICENSE.TXT"
	}
	from("${buildDir}/${libsDirName}") {
		include "${rootProject.name}.jar"
	}
	into "$buildDir/dist"
}
