plugins {
	id('com.github.jk1.dependency-license-report') version '2.1'
	id "org.kordamp.gradle.markdown" version "2.2.0"
	id("com.github.johnrengelman.shadow") version "7.1.2"
	id("io.micronaut.graalvm") version "3.3.2" // Needed to generate Graal resource-config.json
	id("io.micronaut.application") version "3.3.2" apply false
	id("io.micronaut.library") version "3.3.2" apply false
	id 'eclipse'
	id "com.github.ben-manes.versions" version "0.42.0"
	id "org.asciidoctor.jvm.convert" version "3.3.2"
}

version = "0.1"
group = "com.fortify.cli"

apply plugin: "io.micronaut.application"
subprojects {
	apply plugin: "io.micronaut.library"
	apply plugin: 'eclipse'
}

application {
	mainClass.set("com.fortify.cli.app.FortifyCLI")
}
shadowJar {
	archiveBaseName.set('fcli')
	archiveClassifier.set('')
	archiveVersion.set('')
}

allprojects {
	java {
		sourceCompatibility = JavaVersion.toVersion("11")
		targetCompatibility = JavaVersion.toVersion("11")
	}
	micronaut {
		testRuntime("junit5")
		processing {
			incremental(true)
			annotations("com.fortify.cli.*")
		}
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		// Lombok dependency & annotation processor
		compileOnly 'org.projectlombok:lombok:1.18.24'
		annotationProcessor 'org.projectlombok:lombok:1.18.24'

		// Picocli dependency and annotation processor
		annotationProcessor("info.picocli:picocli-codegen:4.6.3")
		
		// TODO Re-enable this line (with appropriate version) once new picocli is released
		//implementation("info.picocli:picocli:4.6.3")

		annotationProcessor("io.micronaut:micronaut-inject-java:3.4.2")
		annotationProcessor("io.micronaut:micronaut-graal:3.4.2")

		// Micronaut dependencies
		implementation("io.micronaut:micronaut-runtime:3.4.2")
		implementation("io.micronaut.picocli:micronaut-picocli:4.1.0")
		implementation("javax.annotation:javax.annotation-api")

		// Logback dependency to allow us to configure logging programmatically
		implementation("ch.qos.logback:logback-classic:1.2.11")

		implementation('com.konghq:unirest-java:3.13.8') {
			exclude group: 'com.google.code.gson', module: 'gson' // We use Jackson, so no need for Gson
		}
		implementation('com.konghq:unirest-objectmapper-jackson:3.13.8')

		implementation('com.jayway.jsonpath:json-path:2.7.0') {
			//exclude group: 'net.minidev', module: 'json-smart'
		}

		// for yaml support and generating trees/tables
		implementation('com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.2')
		implementation('com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.13.2')
		implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.13.2")
		implementation('hu.webarticum:tree-printer:2.0.0')
		implementation('com.github.freva:ascii-table:1.2.0')
		
		// Encryption library
		implementation('org.jasypt:jasypt:1.9.3:lite')
	}
}

// TODO Remove this block once new picocli is released 
configure(allprojects.findAll {it.name != 'fcli-picocli-patches'}) {
	dependencies {
		implementation project(':fcli-picocli-patches')
	}
}

// TODO Remove fcli-picocli-patches condition once new picocli is released
configure(allprojects.findAll {it.name != 'fcli-common' && it.name != 'fcli-picocli-patches'}) {
	dependencies {
		implementation project(':fcli-common')
	}
}

dependencies {
	implementation project(':fcli-config')
	implementation project(':fcli-ssc')
	implementation project(':fcli-fod')
	implementation project(':fcli-sc-sast')
	implementation project(':fcli-sc-dast')
	implementation project(':fcli-run')
	implementation project(':fcli-tool')

	// Logging dependencies
	runtimeOnly('ch.qos.logback:logback-classic:1.2.11')
	runtimeOnly('org.slf4j:jcl-over-slf4j:1.7.36')

	// GraalVM dependency
	compileOnly("org.graalvm.nativeimage:svm")
}

ext {
	gradleHelpersLocation = "https://raw.githubusercontent.com/fortify-ps/gradle-helpers/1.6"
	thirdPartyBaseName = "${rootProject.name}"
	autoCompleteDir = "${project.buildDir}/autocomplete"
}
apply from: "${gradleHelpersLocation}/thirdparty-helper.gradle"
apply from: "${gradleHelpersLocation}/readme2html.gradle"

mainClassName = "com.fortify.cli.app.FCLIRootCommands"

tasks.register('generateAutoCompleteDir') {
    doLast {
        mkdir "${autoCompleteDir}"
    }
}

task generateAutoComplete(type: JavaExec) {
	dependsOn(classes, generateAutoCompleteDir)
    group = "Documentation"
    description = "Generate autocomplete"
    classpath(configurations.runtimeClasspath, configurations.annotationProcessor, sourceSets.main.runtimeClasspath)
    main 'picocli.AutoComplete'
    args mainClassName, "-f", "--completionScript=${autoCompleteDir}/fcli_completion"
}

task generateManpageAsciiDoc(type: JavaExec) {
    dependsOn(classes)
    group = "Documentation"
    description = "Generate AsciiDoc manpage"
    classpath(configurations.runtimeClasspath, configurations.annotationProcessor, sourceSets.main.runtimeClasspath)
    main 'picocli.codegen.docgen.manpage.ManPageGenerator'
    args mainClassName, "--outdir=${project.buildDir}/generated-picocli-docs", "-v"
}

asciidoctor {
    dependsOn(generateManpageAsciiDoc)
    sourceDir = file("${project.buildDir}/generated-picocli-docs")
    outputDir = file("${project.buildDir}/docs")
    logDocuments = true
    outputOptions {
        backends = ['manpage', 'html5']
    }
}

tasks.register('distDocsHtml5', Zip) {
	dependsOn 'asciidoctor'
    archiveFileName = "docs-html.zip"
    destinationDirectory = layout.buildDirectory.dir('dist')
    from layout.buildDirectory.dir("docs/html5")
}

tasks.register('distDocsManPage', Zip) {
	dependsOn 'asciidoctor'
    archiveFileName = "docs-manpage.zip"
    destinationDirectory = layout.buildDirectory.dir('dist')
    from layout.buildDirectory.dir("docs/manpage")
}

task distDocs {
	dependsOn 'distDocsHtml5', 'distDocsManPage'
}

task dist(type: Copy) {
	dependsOn 'build', 'readme2html', 'distDocs', 'generateAutoComplete'
	from "${buildDir}/html"
	from "${autoCompleteDir}"
	from("${projectDir}") {
		include "config/**/*"
		include "LICENSE.TXT"
	}
	from("${buildDir}/${libsDirName}") {
		include "${rootProject.name}.jar"
	}
	into "$buildDir/dist"
}

tasks.register('ConcatenateI18nGeneralMessages') {
	dependsOn subprojects.build // Makes it so that this only runs after all sub-projects/modules have finished building

	doLast {
		println ":: COMBINING GENERAL MESSAGE RESOURCES FILES WITH MODULE SPECIFIC RESOURCE FILES ::"
		def i18nGeneralFiles = fileTree("$buildDir")
				.filter {File f -> f.path ==~ ".*[\\\\/]build[\\\\/]resources[\\\\/]main[\\\\/].*[\\\\/](FCLIGenericMessages(_\\w{2})?.properties\$)"}
		def i18nModuleFiles = fileTree("$rootDir")
				.filter {File f -> f.path ==~ ".*[\\\\/]build[\\\\/]resources[\\\\/]main[\\\\/]com.*[\\\\/](\\w+Messages(_\\w{2})?.properties\$)"}

		for (File moduleFile in i18nModuleFiles){
			try{
				def generalFile = null
				def splitChar = "_"
				if(moduleFile.name.contains(splitChar)){
					def moduleFileEnding = moduleFile.name.split(splitChar)[1]
					generalFile = i18nGeneralFiles.filter {File f -> f.name.endsWith(moduleFileEnding)}.getSingleFile()
				}else{
					// In this case, if splitChar is not in the file name, we expect the the file is for the default locale
					generalFile = i18nGeneralFiles.filter{File f -> f.name.equals("FCLIGeneralMessages.properties")}
							.getSingleFile()
				}
				if(generalFile){
					println "APPENDING $generalFile.name into $moduleFile.name"
					moduleFile.text += ("\n" + generalFile.text)
				}
			}catch(Exception e){}
		}
	}
}
classes.finalizedBy('ConcatenateI18nGeneralMessages')